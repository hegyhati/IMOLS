---
title: "Advanced modelling techniques: big M constraints"
layout: lecture
lead: "Expressing logical conditions in models."
id: modelling_bigM
prerequisite: ["modelling_redundant_variables","gmpl_advanced_1"]
---

<h2 id="motivation">The usual issue</h2>

<p>
  When we are modeling something we often face conditions in the form of <emph>"if something holds, another something should be like this"</emph>.
  Examples could be:
  <ul>
    <li>The domain for the size/capacity/activity level of something is often not just a closed interval, or a simple on/off.  It can be a set of disjoint intervals, a very common situation is: 0 or a \([min,max]\) interval. This decision can not be described by a single decision variable, discrete or continuous. This is usually modelled by a binary \(y\) denoting, whether we are at 0 or in the interval, and then a continuous \(x\), that will hold the actual value. In this case, we have the constraint:<emph>if \(y=0\) then \(x\) should be 0 too, otherwise \(x\in [min,max]\)</emph></li>
    <li>In scheduling we often have constraints in the form of <emph>if two tasks are assigned to the same resource, their processing time should not overlap</emph></li>
    <li>When writing a model, we often end up with bilinear terms, i.e., a product of a continuous and an integer (typically binary) variable. Sucha a model is not linear, however, it can be converted to a linear one by assigning a continuous variable to the product, and say, that if the binary variable is 0, it should be 0, otherwise, it should be equal to the original continuous variable</li>
    <li>Sometimes a binary decision is dependent on another. Let's say it can be decided to which site to sell a worker, and at each site, we can decide, which tasks he/she will perform. Obviously, one cannot perform a task at a location he/she did not visit.</li>
    <!-- TODO : more examples --> 
  </ul>
</p>

<p>
  The problem is, there is no conditional "language element" of MILP models.
  Luckily, there is a modeling trick, that could be applied to express the same thing, as discussed below.
  First, however, let's see an example where this is needed:  
</p>

<h2 id="example">Repairman allocation problem</h2>

<p>
  We provide technical support for some companies located in different cities.
  On each site, there are some issues, that we have to fix today.
  We work with specialist contractors, whom we will call repairmans from now on.
  For each day we have to tell in advance to these repariman whether we will be hiring them for that day, and if we do, where they should go, and which issues should they fix.
  The shift of a repairman can be at most 12 hours, and we have to consider, that the travel overhead of each location is 1 hour in average.
</p>

<p>
  For the moment we will make some assumptions:
  <ul>
    <li>each repairman can fix any of the issues</li>
    <li>an issue is fixed within the same time, regardless of the repairman</li>
    <li>the wage of the repairman are the same</li>
    <li>the traveling time between any two sites is the same</li>
    <li>there is no timing involved, and no dependency between the issues</li>
  </ul>
  Later on, you can generalize the problem by removing some of these assumptions, and alter the model accordingly.
</p>

<p>
  Now, the goal is to minimize the salary we have to pay, that is basically the hourly wage multiplied by all the travel time, and time used for fixing issues.
  Now, let's start making our model
</p>

<p id="model">
  The input data for the problem is given with these sets and parameters:
  <pre>
    set Sites;
    param traveltime;
    set Issues;
    param site{Issues} symbolic, within Sites;
    param time{Issues} >=0;
    set Repairmen;
    param maxhours;
    param wage;
  </pre>  
</p>

<p>
  The variables are self evident:
  <pre>
    var visits{Repairman,Sites} binary;
    var fixes{Repairman,Issues} binary;
  </pre>
</p>

<p>
  As for the constraints: first, each and every one of the issues need to be fixed:
  <pre>
    s.t. FixAllIssues{i in Issues}:
      sum{r in Repairmen} fixes[r,i] = 1;
  </pre>
  A repairman can not work more than the given limit:
  <pre>
    s.t. MaxWorkLoad{r in Repairmen}:
      sum{s in Sites} (traveltime*visits[r,s]) + sum{i in Issues}(time[i]*fixes[r,i]) <= maxhours;
  </pre>
</p>


<p>
  The objective is simple, minimizing the total cost, which will be pretty similar to the lhs of the last constraint, only summed for all of the repairmen:
  <pre>
    minimize TotalCost:
      wage * sum {r in Repairmen} (sum{s in Sites} (traveltime*visits[r,s]) + sum{i in Issues}(time[i]*fixes[r,i]));
  </pre>
</p>

<p>
  If we solver this model, we recognize, that all of the <code>visits[r,s]</code> variables are 0 in the optimal solution.
  The reason for that is, that we have not expressed yet in any of the constraints, that:
  <emph>A repairman can only fix an issue, if he/she visits its site.</emph>
</p>

<p>
  Translating that to our notation, it means, that: <code>fixes[r,i]</code> can only be 1, if <code>visits[r,site[i]]</code> is also 1.
  Maybe the following equivalent statement from opposite direction is more straight forward: <emph>if a repairman does not visit a site, he/she can not fix any of the issues there.</emph>, i.e., if <code>visits[r,s]==0</code> then <code>fixes[r,i]</code> must be 0 as well for all <code>i</code> such that <code>site[i]==s</code>.
  We could be tempted to express this in the following way:
  <pre>
    s.t. NoMagicalTeleportation{r in Repairmen, s in Sites, i in Issues : visits[r,s]==0 && site[i]==s}:
      fixes[r,i]=0;
  </pre>
  The problem with this constraint is, that the filtering is based not only on "static" parameters and sets, but "dynamic" variables as well.
  This would mean, that depending on the values of the variables, some constraints are added/removed during the optimization procedure.
  Well, that's not how MILP solvers work, and <code>glpsol</code> will give us back an error message.
</p>

<p>
  So we have to find another way.
  Let's start by putting our idea down in an informal way:
  <pre>
    s.t. NoMagicalTeleportation{r in Repairmen, s in Sites, i in Issues: site[i]==s}:
      if visits[r,s]==0 then fixes[r,i]=0;
  </pre>
  First of all, there is no need for the <code>s in Sites</code> part, as there will always be only a single <code>s</code> for each <code>i</code>:
  <pre>
    s.t. NoMagicalTeleportation{r in Repairmen, i in Issues}:
      if visits[r,site[i]]==0 then fixes[r,i]=0;
  </pre>
  The core idea for our trick is to include the "deciding" binary variable, <code>visits[r,site[i]]</code> into the constraint <code>fixes[r,i]=0</code> in such a way, that when it is 0, then it "disappears", and when it is 1, the constraint becomes "relaxed", meaning, that it will not actually state anything. 
</p>

<p>
  Relaxing an equality is kind of troublesome.
  So when possible, we should try to figure out, whether we can state the same with an inequality or not.
  In the worst case, an \(lhs=rhs\) equality can be transformed to the conjunction of two inequalities: \(lhs\ge rhs\) and \(lhs\le rhs).
  However, in this case, the following will simply do:
  <pre>
    s.t. NoMagicalTeleportation{r in Repairmen, i in Issues}:
      if visits[r,site[i]]==0 then fixes[r,i]<=0;
  </pre>
  Since <code>fixes[r,i]</code> can only be 0 or 1, "equals to 0" is equivalent to "is equal or less than 0".
  Now, <strong>drumroll</strong>, <code>visits[r,site[i]]</code> can be included in the constraint like this:
  <pre>
    s.t. NoMagicalTeleportation{r in Repairmen, i in Issues}:
      fixes[r,i]<=0          + visits[r,site[i]];
  </pre>
</p>

<p>
  Now, let us examine this constraint:
  <ul>
    <li>
      If <code>visits[r,site[i]]</code> is 0, then it "disappears" from the right side, and the constraint <code>fixes[r,i]<=0</code> remains, as it should.
    </li>
    <li>
      If <code>visits[r,site[i]]</code> is 1, then the RHS becomes 1, and the constraint is essentially <code>fixes[r,i]<=1</code>. Since <code>fixes[r,i]</code> is a binary variable, that can be at most 1, this constraint doesn't really say anything, as intended.
    </li>
  </ul>

  Az we can see, the above constraint does exactly what we needed, and implements that conditional-like behaviour.
  Of course, it could be simplified like this:
  <pre>
    s.t. NoMagicalTeleportation{r in Repairmen, i in Issues}:
      fixes[r,i]<=visits[r,site[i]];
  </pre>
</p>

<h3 id="pimp">Pimping the model a little bit</h3>

<p>
  Putting together everything from above, we have:
  <pre>
    set Sites;
      param traveltime;
    set Issues;
      param site{Issues} symbolic, within Sites;
      param time{Issues} >=0;
    set Repairmen;
      param maxhours;
      param wage;

    var visits{Repairman,Sites} binary;
    var fixes{Repairman,Issues} binary;

    s.t. FixAllIssues{i in Issues}:
      sum{r in Repairmen} fixes[r,i] = 1;

    s.t. MaxWorkLoad{r in Repairmen}:
      sum{s in Sites} (traveltime*visits[r,s]) + sum{i in Issues}(time[i]*fixes[r,i]) <= maxhours;

    s.t. NoMagicalTeleportation{r in Repairmen, i in Issues}:
      fixes[r,i]<=visits[r,site[i]];

    minimize TotalCost:
      wage * sum {r in Repairmen} (sum{s in Sites} (traveltime*visits[r,s]) + sum{i in Issues}(time[i]*fixes[r,i]));
  </pre>
</p>

<p>
  Now let us assume a more realistic scenario: if we hire someone for the day, we will have to pay him/her at least 8 hours work, even if a single issue is solved in 3 hours for example. 
</p>

<p>
  To implement this, first it is benefitial to introduce a redundant variable describing, how much a person has actually worked.
  This will also simplify the <code>MaxWorkLoad</code> constraint and the objective function:

  <pre>
    set Sites;
      param traveltime;
    set Issues;
      param site{Issues} symbolic, within Sites;
      param time{Issues} >=0;
    set Repairmen;
      param maxhours;
      param wage;

    var worked{Repairmen} >= 0;  # <--- NEW REDUNDANT VARIABLE
    var visits{Repairman,Sites} binary;
    var fixes{Repairman,Issues} binary;

    s.t. FixAllIssues{i in Issues}:
      sum{r in Repairmen} fixes[r,i] = 1;

    s.t. Workload{r in Repairmen}: #<--- new constraint to set the new variable
      worked[r] = sum{s in Sites} (traveltime*visits[r,s]) + sum{i in Issues}(time[i]*fixes[r,i])
      
    s.t. MaxWorkLoad{r in Repairmen}: # <--- SIMPLIFIED CONSTRAINT
       worked[r] <= maxhours;

    s.t. NoMagicalTeleportation{r in Repairmen, i in Issues}:
      fixes[r,i]<=visits[r,site[i]];

    minimize TotalCost: # <--- SIMPLIFIED OBJECTIVE
      wage * sum {r in Repairmen} worked[r];
  </pre>
</p>

<p>
  Now, we should somehow implement the "at least 8 hours rule".
  The first idea could be to change the declaration of the new variable like this:
  <pre>
    var worked{Repairmen} >= 8;
  </pre>
  This would, however require all of the repairman to work at least 8 hours, which is double-bad:
  <ol>
    <li>Not everyone should work 8 hours.</li>
    <li>The rule said nothing about working at least 8 hours, only about paying at least 8 hours.</li>
  </ol>
</p>

<p>
  From the second point above, it is straight forward, that we need a new variable, that tells, how much is payed to a person:
  <pre>
    var payed{Repairmen} >=0;
  </pre>
  Then the objective can easily be changed:
  <pre>
    minimize TotalCost: 
      sum {r in Repairmen} payed[r];
  </pre>
  And for the moment, lets's connect <code>payed[r]</code> with <code>worked[r]</code> like this:
  <pre>
    s.t. Payment{r in Repairman}:
      payed[r]=wage*worked[r];
  </pre>
  With all these changes, the model does exactly the same so far. 
</p>

<p>
  However, now we have the opportunity to implement the "at least 8 hours" rule.
  First, let us introduce the new parameter:
  <pre>
    param minhourspayed;
  </pre>
  Then we can say in the cariable declaration, that:
  <pre>
    var payed{Repairmen} >= wage * minhourpayed;
  </pre>
  And we need to relax the above constraint like this:
  <pre>
    s.t. Payment{r in Repairman}:
      payed[r]>=wage*worked[r];  # <--- GREATER-EQUAL INSTEAD OF EQUAL
  </pre>
</p>

<p>
  What we achieved like this is that <code>payed[r]</code> is now greater or equal than <code>wage*worked[r]</code> and <code>wage * minhourpayed</code>.
  So basically, it is at least as much as the maximum of the two.
  <div class="alert alert-info">
    Note, that <code>payed[r]</code> can now be larger than <code>wage*worked[r]</code> even if <code>worked[r]>minhourspayed</code>.
    Basically, the model allows to pay more to a person than he/she deserved/worked for.
    This is, however not a problem, as we want to minimize the costs, so such solutions, even though feasible, would not lead to optimal solutions.
  </div>
</p>

<p>
  Another problem still remains, however.
  This way, everyone needs to be payed at leasy 8 hours, even those who are not hired for the day.
  To put it this way: now <code>payed[r]</code> is always greater-or equal than <code>wage*worked[r]</code>, which is fine, but it is also always greater-or equal than <code>wage * minhourpayed</code>, which is not.
  We should remove that initial bound from the variable declaration, and have a constraint like this instead:
  <pre>
    var payed{Repairmen} >= 0; # <--- CHANGED LOWER BOUND TO 0

    ...

    s.t. MinimumWorkHours{r in Repairman}:
      if r is hired for the day then: payed[r] >= wage * minhourpayed;
  </pre>
</p>

<p>
  Now we have a new problem (we just can't run out of them it seems), that we have no expression describing, whether someone is hired or not.
  A general rule of thumb: if there is no easy expression for something, it should probably be introduced as a variable, even if it is redundant:
  <pre>
    var payed{Repairmen} >= 0;
    var hired{Repairmen} binary;

    ...

    s.t. MinimumWorkHours{r in Repairman}:
      if hired[r]==1 then: payed[r] >= wage * minhourpayed;
  </pre>
  Since the RHS is only a constant expression (no variables included), we can easily "resolve" this if:
  <pre>
    var payed{Repairmen} >= 0;
    var hired{Repairmen} binary;

    ...

    s.t. MinimumWorkHours{r in Repairman}:
      payed[r] >= wage * minhourpayed * hired[r];
  </pre>
  It is easy to see, that if <code>r</code> is hired, the intended constraint holds, otherwise, it just says, that <code>payed[r]>=0</code>, which again, doesn't say anything at all.
</p>

<p>
  We are not ready yet though, since <code>hired[r]</code> has no connection with the other binary variables, so the model allows a repairman not to be hired while still solving issues.
  What we would like to express: <emph><code>r</code> can only solve issues or travel to sites if he/she is hired for the day</emph>.
  Putting it to a more formal way: <code>visits[r,s]</code> or <code>fixes[r,i]</code> can only be 1, if <code>hired[r]=1</code>.
  Again, it may be simpler to look at it from the other direction: if <code>hired[r]==0</code> then all  <code>visits[r,s]</code>,<code>fixes[r,i]</code> variables must be 0 as well:

  <pre>
    s.t. CannotWorkIfNotHired{r in Repairmen}:
      if hired[r]==0 then all visits[r,s],fixes[r,i] must be 0;
  </pre>
</p>

<p>
  There is an easy way of telling that a bunch of binary variables should be 0: their sum should be 0, or (since inequality is more loved - see above) less or equal than 0.
  So:
  <pre>
    s.t. CannotWorkIfNotHired{r in Repairmen}:
      if hired[r]==0 then sum{s in Sites }visits[r,s] + sum {i in Issues}fixes[r,i] <= 0;
  </pre>
</p>

<p>
  Now we only need to resolve that conditional.
  The RHS should be 0 if <code>hired[r]</code> is 0, otherwise, it should be a big number to make the constraint "meaningless".
  So something like this would do:
  <pre>
    s.t. CannotWorkIfNotHired{r in Repairmen}:
      sum{s in Sites }visits[r,s] + sum {i in Issues}fixes[r,i] <= ReallyBigNumber * hired[r];
  </pre>
  We can see, that this equation would do exactly what we want it to do, we just have to give a value to <code>ReallyBigNumber</code>, for example:
  <pre>
    param ReallyBigNumber := 1000;
  </pre>
</p>

<p>
  But how do we know that 1000 is enough?
  What if we have 100000 sites and 99999999999 issues?
  Well, our model would probably never be solved on current machines for start, but putting that aside, even if <code>r</code> is hired, He could work on at most 1000 issues, regardless how quick they are.
  In practice, selecting such a value is usually easy, as we know roughly the range of other parameters, and the context in which it will be used.
  But, for the mathematician-hearted people that is not a satisfactory response.
  We can often be theoretically correct too.
  Here the sum of the sizes of the sets <code>Sites</code> and <code>Issues</code> is a good-enough value.
  And that can actually be expressed in GMPL:
  
  <pre>
    param ReallyBigNumber := card(Sites)+card(Issues);
  </pre>
</p>

<p>
  One final touch: this parameter is often denoted with <code>M</code>, a capital/big M, that is large/big.
  Thus, these kind of constraints are often referred to as <emph>big-M constraints</emph>.
</p>

<h3 id="finalmodel">The final model</h3>

<p>
  <pre>
    set Sites;
      param traveltime;
    set Issues;
      param site{Issues} symbolic, within Sites;
      param time{Issues} >=0;
    set Repairmen;
      param maxhours;
      param wage;

    param M:=card(Sites)+card(Issues)

    var worked{Repairmen} >= 0;
    var payed{Repairmen} >= 0;
    
    var hired{Repairmen} binary;

    var visits{Repairman,Sites} binary;
    var fixes{Repairman,Issues} binary;

    s.t. FixAllIssues{i in Issues}:
      sum{r in Repairmen} fixes[r,i] = 1;

    s.t. Workload{r in Repairmen}: 
      worked[r] = sum{s in Sites} (traveltime*visits[r,s]) + sum{i in Issues}(time[i]*fixes[r,i])
      
    s.t. MaxWorkLoad{r in Repairmen}:
       worked[r] <= maxhours;
       
    s.t. Payment{r in Repairman}:
      payed[r]=wage*worked[r];

    s.t. NoMagicalTeleportation{r in Repairmen, i in Issues}:
      fixes[r,i]<=visits[r,site[i]];

    s.t. MinimumWorkHours{r in Repairman}:
      if hired[r]==1 then: payed[r] >= wage * minhourpayed;

    s.t. CannotWorkIfNotHired{r in Repairmen}:
      sum{s in Sites }visits[r,s] + sum {i in Issues}fixes[r,i] <= M * hired[r];
      
    minimize TotalCost: 
      sum {r in Repairmen} payed[r];
  </pre>
</p>

<h2 id="generalforms">Several forms of big-M constraints / logical conditionals</h2>

<p>
  In the example above we have seen several different ways of implementing logical conditionals in simple linear constraints.
  Here some of the most commonly used ones are summarized without aiming for any kind of completeness.
  It is always assumed that \(y,y',y^*,y_1,y_2,\dots,y_n\) are a binary variables.
</p>

<div class="alert alert-primary">
  <ul>
    <li> If \(y=1\) then \(LHS \ge RHS\): \[ LHS \ge RHS - M \cdot (1-y)\] </li>
    <li> If \(y=0\) then \(LHS \ge RHS\): \[ LHS \ge RHS - M \cdot y\] </li>
    <li> If \(y=1\) then \(LHS = RHS\): \[ LHS \ge RHS - M \cdot (1-y)\] \[ LHS \le RHS + M \cdot (1-y)\]</li>
    <li> If \(y=0\) then \(y'\) must be 0: \[y' \le y\]</li>
    <li> If \(y=1\) then \(y'\) must be 0: \[y' \le 1-y\]</li>
    <li> If \(y=0\) then \(y_1,y_2,\dots,y_n\) must be 0: \[y_1+y_2+\dots+y_n \le M \cdot y\]</li>
    <li> If \(y=0\) then \(y_1,y_2,\dots,y_n\) must be 1: \[y_1+y_2+\dots+y_n \ge n \cdot (1- y)\]</li>
    <li> If \(y=1\) and \(y'=1\) then \(LHS \ge RHS\): \[ LHS \ge RHS - M \cdot (2-y-y')\] </li>
    <li> If \(y=0\) and \(y'=0\) then \(LHS \ge RHS\): \[ LHS \ge RHS - M \cdot (y+y')\] </li>
    <li> If \(y=0\) and \(y'=1\) then \(LHS \ge RHS\): \[ LHS \ge RHS - M \cdot (y+ (1-y')\] </li>
    <li> If \(y=1\) and \(y'=0\) and \(y^*=1\) then \(LHS \ge RHS\): \[ LHS \ge RHS - M \cdot ( (1-y) +(y') +(1-y^*))\] </li>
    <li> \(y^*\) must be 1 if and only if \(y=1\) and \(y'=1\) \[ y^* \ge -1 + y + y' \] \[y^* \le \frac{y+y'}{2} \]</li>
  </ul>
</div>

<h2 id="bilinear">Bilinear terms</h2>
<p>
  A bilinear term is something like \(y \cdot x\) where \(y\) and \(x\) are both variables.
  If one of the terms is binary, and the other is, for example, a non-negative continuous variable, the model can be linearized using big M constraints, as follows.
  The term itself should be replaced by a new non-negative continuous variable \(z\), and the model should be extended with the following constraints:
  \[z \le x \]
  \[z \le M \cdot y\]
  \[z \ge x - M \cdot (1-y)\]
</p>
<p>
  <ol>
    <li>If \(y=1\) then \(z\) should just be \(x\). In this case, the above equations reduce to this:
        \[z \le x \]
        \[z \le M \]
        \[z \ge x \]
        The second doesn't do anything, the other two sets \(z\) to \(x\).
    </li>
    <li>If \(y=1\) then \(z\) should just be 0. In this case, the above equations reduce to this:
        \[z \le x \]
        \[z \le 0 \]
        \[z \ge -M  (+x) \]
        The second is stronger than the first, and sets \(z\) to 0. The third doesn't do anything.
    </li>
  </ol>
</p>

<h2 id="efficiency">A note on efficiency</h2>

<p>
  Big-M constraints can slow down the solution of an MILP problem, as they usually do not contribute well to the bounding of a subproblem.
  Thus, as a general thumb of rule: only use such constraints when necessary, and if so, try not to use unnecessarily big values in the constraints.
  Like don't write \(y'\le 1000 \cdot y\) instead of just \(y'\le y\).
  It is not guaranteed, that big-M constraints will make Your model slow, sometimes they don't, but it is generally good to avoid them if possible.
</p>

<hr />

<div class="alert alert-dark">
  
<h1 class="alert-heading" id="final-notes">Final notes</h1>

The goal of this lecture was to introduce a modeling technique that allows seemingly unmodellable conditionals to be translated to standard linear constraints.
In practice, this techniques is used very extensively, as it is often needed.

</div>
